PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX npdv: <http://sws.ifi.uio.no/vocab/npd-v2#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?wellbore (?length AS ?lenghtM) ?company ?year 
WHERE {
  ?wc npdv:coreForWellbore
  [ rdf:type                      npdv:Wellbore ;
    npdv:name                     ?wellbore ;
    npdv:wellboreCompletionYear   ?year ;
    npdv:drillingOperatorCompany  [ npdv:name ?company ] 
  ] .
  {
    { ?wc npdv:coresTotalLength ?l ;
      npdv:coreIntervalUOM "[m   ]"^^xsd:string . # Meters
      BIND(?l AS ?length)
    } 
    UNION
    { ?wc npdv:coresTotalLength ?l ;
      npdv:coreIntervalUOM "[ft   ]"^^xsd:string . # Feets
      BIND((?l * 0.3048) AS ?length) 
    }
  }
  FILTER(?year >= "2008"^^xsd:integer && 
	 ?length > 50 )
} ORDER BY ?wellbore
